buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${version_major}.${version_minor}.${version_patch}"
group = "snownee.research"
archivesBaseName = "ResearchTable"

if (System.getenv().BUILD_NUMBER != null) {
    version += "-build${System.getenv().BUILD_NUMBER}"
}

sourceCompatibility = targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven {
        name = "jei"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = 'tterrag maven'
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = 'stages'
        url = 'https://maven.mcmoddev.com'
    }
    maven {
        name = "jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "tehnut"
        url = "https://maven.tehnut.info"
    }

    flatDir {
        dirs 'libs'
    }
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = "${mapping_version}"
    useDepAts = true

    replace "@VERSION_INJECT@", project.version
}

dependencies {
    // Why not deobfCompile? Since JEI 4.13.1, JEI uses stable_39 mapping, so we are on the same MCP version now.
    compile "mezz.jei:jei_${minecraft_version}:${jei_version}"

    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${ct_version}"
    deobfCompile "CraftTweaker2:CraftTweaker2-API:${ct_version}"
    deobfCompile "CraftTweaker2:ZenScript:${ct_version}"
    compile "net.darkhax.bookshelf:Bookshelf-1.12.2:${bookshelf_version}"
    compile "net.darkhax.gamestages:GameStages-1.12.2:${gamestages_version}"
    compile "net.darkhax.itemstages:ItemStages-1.12.2:${itemstages_version}"
    //deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
    deobfCompile "codersafterdark.reskillable:Reskillable:1.12.2-1.12.5-PR-156.1"
    //deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:${top_version}") {
    //    exclude group: 'cofh'
    //}
    compile files(
        'Kiwi-1.12.2-0.5.3.32-deobf.jar',
        'GrandEconomy-1.12.2-1.0.1-deobf.jar'
    )
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.d"
    exclude "**/*.exe"
    exclude "**/*.e"
    exclude "**/*.txt"
    manifest {
        attributes 'FMLAT': 'research_at.cfg'
        attributes 'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}"
        attributes 'Timestamp': System.currentTimeMillis()
    }
}

javadoc {
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    options.links = [
            'https://docs.oracle.com/javase/8/docs/api/'
    ]
    options.tags = [
            "implSpec:a:Implementation Specification",
            "implNote:a:Implementation Note"
    ]
}
